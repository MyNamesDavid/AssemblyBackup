        include Win64API.inc

        .DATA

;---------------------
; EQUATES 
;---------------------

MAX_RAM             EQU     1024                            ;Maximum size of emulated CPU's RAM
INVALID_HANDLE      EQU     -1                              ;CreateFile returns this value if it failed
_CR                 EQU     0Dh                             ;Carriage return character
_LF                 EQU     0Ah                             ;Line Feed (new line) character
NULL_PTR            EQU     0
ERROR               EQU     1                               ;return code indicating an error occurred
READ_FILE_ERROR     EQU     0                               ;ReadFile will return 0 if an error occurred

;---------------------
;variables
;---------------------

errMsgFileOpen      byte    "ERROR:  Unable to open input file", _CR, _LF

filename            byte    "machine.bin", NULL            ;file name must be null terminated

programBuffer       byte    MAX_RAM dup (0)                 ;max size of RAM 1K

returnCode          dword   0                               ;used to return program status back to OS

bytesWritten        dword   0
BytesRead           dword   0                               ;number of bytes read from file will be stored here
fileHandle          qword   0                               ;handle to file containing program
fileSize            dword   0                               ;size of file
hStdOut             qword   0                               ;handle to the standard output
hStdIn              qword   0                               ;handle to the standard input

Registers           byte    6 dup(00h)                        ;Array used to emulate 8 bit registers 
NumCharsWritten     dword   0                                 ;number of characters written to console

;*********************
;Comparison bytes
;*********************

ADDInstruction		equ		11h
SUBInstruction		equ		22h
XORInstruction		equ		44h
LOADInstruction		equ		05h
LOADRInstruction	equ		55h
STOREInstruction	equ		06h
STORRInstruction	equ		66h		
OUTInstruction		equ		0CCh
JNZInstruction		equ		0AAh
HALTInstruction		equ		0FFh
;*********************
;Other Equates
;*********************

ADDRESSIncrement	equ		02h
ZERO				equ		00h

                    .CODE

Main                Proc

                    sub     rsp,40                          ;shadow memory and align stack
                                                            ;32 bytes for shadow memory
                                                            ; 8 bytes to align stack

                    ;*********************************
                    ; Get Handle to Standard output
                    ;*********************************
                    mov     ecx,STD_OUTPUT_HANDLE           ;pass handle to get in ecx
                    call    GetStdHandle                    ;call Windows API
                    mov     hStdOut,rax                     ;save returned handle

                    ;*********************************
                    ; Get Handle to Standard input
                    ;*********************************
                    mov     ecx,STD_INPUT_HANDLE            ;pass handle to get in ecx
                    call    GetStdHandle                    ;call Windows API
                    mov     hStdIn,rax                      ;save returned handle

                    ;*********************************
                    ; Open existing file for Reading
                    ;*********************************
                    mov     rcx,offset fileName             ;name of file to open
                    mov     rdx,GENERIC_READ                ;
                    mov     r8,FILE_SHARE_NONE              ;file sharing - NONE
                    mov     r9,NULL_PTR                     ;
                    mov     qword ptr [rsp+32],OPEN_EXISTING            ;file must exist
                    mov     qword ptr [rsp+40],FILE_ATTRIBUTE_NORMAL    ;file attribute - normal
                    mov     qword ptr [rsp+48],NULL_PTR                 ;
                    call    CreateFileA
                    cmp     eax,INVALID_HANDLE              ;was open successful?
                    je      OpenError                       ;No....Display error and Exit
                    mov     fileHandle,rax                  ;Yes...then save file handle

                    ;********************************************
                    ; Determine the size of the file (in bytes)
                    ;********************************************
                    mov     rcx,fileHandle                  ;handle of open file
                    mov     rdx,NULL_PTR                    ;
                    call    GetFileSize                     ;Windows API function - returns file size
                    mov     fileSize, eax

                    ;********************************************
                    ; Make sure the size of the file doesn't 
                    ; exceed our buffer size. If it does then exit
                    ;********************************************
                    cmp     fileSize,LENGTHOF programBuffer ;Is file size greater than our buffer?
                    jc      ReadFromFile                    ;no...then read the entire file into our buffer
                    mov     returnCode,ERROR                ;yes..set return code to error
                    jmp     CloseFile                       ;     and exit

                    ;****************************************
                    ; Read the entire file into emulator RAM
                    ;****************************************
ReadFromFile:
                    mov     rcx,fileHandle                  ;handle to the file to read
                    mov     rdx,offset programBuffer        ;where to put data read
                    mov     r8d,fileSize                    ;number of bytes to read
                    mov     r9,offset bytesRead             ;returns bytes read in this variable
                    xor     rax,rax                         ;last parameter is 0
                    mov     [rsp+32],rax                    ;parameters > 4 are passed on the stack
                    call    ReadFile                        ;read the entire file into programBuffer
                    cmp     eax,READ_FILE_ERROR             ;was read successful?
                    jne     RunProgram                      ;Yes..then execute the program
                    mov     returnCode,ERROR                ;no...set return code to error
                                                            ;     and close file and exit
                    ;*********************************
                    ; Close the file
                    ;*********************************
CloseFile:
                    mov     rcx,fileHandle                  ;pass in handle to the file to close
                    call    CloseHandle
                    jmp     Finish

OpenError:
                    ;Let user know there was an error opening the file
                    mov     rcx,hStdOut                     ;1st parameter - handle of where to send message
                    mov     rdx,OFFSET errMsgFileOpen       ;2nd parameter - message to display
                    mov     r8,LENGTHOF errMsgFileOpen      ;3rd parameter - number of characters to display
                    mov     r9,OFFSET bytesWritten          ;4th parameter - pointer where bytes written will be stored
                    xor     rax,rax                         ;last parameter is 0
                    mov     [rsp+32],rax                    ;parameters > 4 are passed on the stack
                    call    WriteConsoleA                   ;display the error message
                    mov     returnCode,ERROR                ;let caller know there was an error
                    jmp     finish                          ;exit program

RunProgram:
;********************
;Set Up the Buffer
;********************
				
					lea		r10, Registers			;Get the address of the emulated registers R0-R5
					lea     r11, programBuffer		;Get the address of the programBuffer
					lea		r12, programBuffer		;Get the address of the start of the programBuffer

;********************************************************
;Compare the bytes found in the machine.bin with the 
;opcode valueto decide which which lable to jump to
;********************************************************

Comparison:

					movzx   rbx, byte ptr [r11]		;Move the value used to compare the opcode is a movzx to clear out the register				
					cmp		bl, ADDInstruction		;Compare the opcode with the intructions if equal jump to the corresponding lable
					je		ADDLable

					mov     bl, [r11]				;Move the value used to compare the opcode
					cmp		bl, SUBInstruction		;Compare the opcode with the intructions if equal jump to the corresponding lable
					je		SUBLable

					mov     bl, [r11]				;Move the value used to compare the opcode
					cmp		bl, XORInstruction		;Compare the opcode with the intructions if equal jump to the corresponding lable
					je		XORLable

					mov     bl, [r11]				;Move the value used to compare the opcode
					cmp		bl, LOADInstruction		;Compare the opcode with the intructions if equal jump to the corresponding lable
					je		LOADLable

					mov     bl, [r11]				;Move the value used to compare the opcode
					cmp		bl, LOADRInstruction	;Compare the opcode with the intructions if equal jump to the corresponding lable
					je		LOADRLable

					mov     bl, [r11]				;Move the value used to compare the opcode
					cmp		bl, STOREInstruction	;Compare the opcode with the intructions if equal jump to the corresponding lable
					je		STORELable

					mov     bl, [r11]				;Move the value used to compare the opcode
					cmp		bl, STORRInstruction	;Compare the opcode with the intructions if equal jump to the corresponding lable
					je		STORRLable

					mov     bl,  [r11]			    ;Move the value used to compare the opcode
					cmp		bl,	 OUTInstruction		;Compare the opcode with the intructions if equal jump to the corresponding lable
					je		OUTLable

					mov     bl, [r11]			    ;Move the value used to compare the opcode
					cmp		bl, JNZInstruction		;Compare the opcode with the intructions if equal jump to the corresponding lable
					je		JNZLable

					mov     bl, [r11]			    ;Move the value used to compare the opcode
					cmp		bl, HALTInstruction		;Compare the opcode with the intructions if equal jump to the corresponding lable
					je		Finish

;****************************************************************
;Mnemonic	OpCode(Hex)	Operand 1	Operand 2
;	
;ADD	            11h 	Reg1	Reg2     Reg1 = Reg1 + Reg2
;        
;****************************************************************
					
ADDLable:		
					inc		r11							;Move to register 1
					movzx	rbx, byte ptr [r11]			;Determine which register to use for register 1
					inc		r11							;Move to register 2
					movzx	rcx, byte ptr [r11]			;Determine which register to use for register 2
					mov		cl, [r10+rcx]				;Move the value of register 2 into a temp register
					add		[r10+rbx], cl				;Reg1 + Reg2 = Reg 1
					inc		r11

					jmp		Comparison

;***************************************************************
;Mnemonic	OpCode(Hex)	Operand 1	Operand 2
;			
;SUB	         22h	Reg1	    Reg2	Reg1 = Reg1 - Reg2
;***************************************************************

					
SUBLable:		
					inc		r11							;Move to register 1
					movzx	rbx, byte ptr [r11]			;Determine which register to use for register 1
					inc		r11							;Move to register 2
					movzx	rcx, byte ptr [r11]			;Determine which register to use for register 2
					mov		cl, [r10+rcx]				;Move the value of register 2 into a temp register
					sub		[r10+rbx], cl				;reg1 = reg1 - reg2
					inc		r11

					jmp		Comparison

;***************************************************************
;Mnemonic	OpCode(Hex)	Operand 1	Operand 2
;XOR     	44h	            Reg1	Reg2	Reg1 = Reg1 XOR Reg2	
;****************************************************************

XORLable:		
					inc		r11							;Move to register 1
					movzx	rbx, byte ptr [r11]			;Determine which register to use for register 1 
					inc		r11							;Move to register 2
					movzx	rcx, byte ptr [r11]			;Determine which register to use for register 2
					mov		cl, [r10+rcx]				;add the value of register 2 with the value obtained from register 1
					xor		[r10+rbx], cl				;XOR the value stored in Register 1 with the value in register 2
					inc		r11

					jmp		Comparison

;***************************************************************************************
;Mnemonic	OpCode(Hex)	Operand 1	Operand 2
;LOAD 		05h			Reg1					Address	Load Reg1 with value at Address
;***************************************************************************************

LOADLable:
					inc		r11							;Increments from the op code into the register we have to use
					movzx	rbx, byte ptr [r11]			;Determine which register to use for register 1
					inc		r11							;Move to the address that we need to use
					movzx	rcx, word ptr [r11]			;Move the address into a 16 bit register
					xchg	ch, cl						;Exhange the values in ch cl from big endian to little endian
					movzx	rdx, byte ptr [r12 + rcx]	;Move to the correspoinding memory address and put the value in the register [ProgramBuffer +address]
					mov		[r10+rbx], dl				;Move the value found a the address into the emulated register
					add		r11, ADDRESSIncrement

					jmp		Comparison

;***************************************************************************************
;Mnemonic	OpCode(Hex)	Operand 1	Operand 2
;LOADR 	     55h	    Reg1	     Address	Load Reg1 with value at (Address + Reg1)
;***************************************************************************************

LOADRLable:
					inc		r11							;Increments from the op code into the register we have to use
					movzx	rbx, byte ptr [r11]			;Determine which register to use for register 1
					inc		r11 						;Move to the address that we need to use
					movzx	rcx, word ptr [r11]			;Move the address into a 16 bit register
					xchg	ch, cl						;Exhange the values in ch cl from big endian to little endian
					
					add		rcx, r12					;Add the address of the program buffer and the address that we want
					movzx	rdx, byte ptr [r10 + rbx]	;Move the value of the emulated register in to a register
					add		rcx, rdx					;[ProgramBuffer + Address + register value]
					mov		dl, [rcx]					;Store the value of the combined registers in a new register
					mov		[r10+rbx], dl				;Store the value in the correct register
					add		r11, ADDRESSIncrement
					
					jmp		Comparison

;***************************************************************************************
;Mnemonic	OpCode(Hex)	Operand 1	Operand 2
;STORE   	06h	        Address		            Write value in R0 to Address
;***************************************************************************************

STORELable:	
	
					inc		r11							;Points to the address of the operand
					movzx	rbx, word ptr [r11]			;Determine which register to use for register 1
					xchg	bh, bl						;Change big Endian into little endian

					movzx	rcx, byte ptr [r10]			;Put the Value of R0 into a register
					mov		[r12 + rbx], cl				;Store the value of R0 into the correct address

					add		r11, ADDRESSIncrement

					jmp		Comparison

;***************************************************************************************
;Mnemonic	OpCode(Hex)	Operand 1	Operand 2
;STORR	     66h	    Reg1	    Address	   Write value in R0 to (Address + Reg1)
;***************************************************************************************

STORRLable:	

					inc		r11							;Points to the address of the operand
					movzx	rbx, byte ptr [r11]			;Determine which register to use for register 1
					inc		r11
					mov		cx, [r11]					;Puts the address of the instruction into bx
					
					xchg	ch, cl						;Change big Endian into little endian
														
					
					add		rcx, r12 					;Add the address of the program buffer and the address that we want
					movzx	rdx, byte ptr [r10 + rbx]	;Move the value of the emulated register in to a register
					add		rcx, rdx					;rcx = [programBuffer + address + Register value]
					
					mov		dl, [r10]					;move the value within R0 to a register 

					mov		[rcx], dl					;Store the value of R0 into the correct address

					add		r11, ADDRESSIncrement

					jmp		Comparison

;***************************************************************************************
;Mnemonic	OpCode(Hex)	Operand 1	Operand 2zz
OUTLable:;TODO	DONE?			
					 inc		r11
					movzx	rbx, byte ptr [r11]					;Determine which register to use for register 1
					

					 xor        rax, rax                                ;last parameter is 0
					 mov        [rsp+32], rax                           ;parameters > 4 are passed on the stack
					 mov        r9, offset NumCharsWritten               ;Number of characters written will be returned here
                     mov        r8d, TYPE Registers						  ;Number of bytes to write to console
                     mov        rdx, offset	Registers                    ;Address of data to write to console
					 add		rdx, rbx								;Move to correct Register
                     mov        rcx, hStdOut                              ;Handle where to write
					 push		r10							;Perserve emulated address registers
					 push		r11										;Perserve the data in the stack
                     call       WriteConsoleA
					 pop		r11
					 pop		r10
					 inc		r11
					 jmp		Comparison
JNZLable:				
					 inc		r11							;Move to register 1
					 movzx		rbx, byte ptr [r11]			;Determine which register to use for register 1
					 mov		bl, [r10+rbx]				;move the value of register and move the value to al
					 inc		r11							;move to the next instruction
					 movzx	    rcx, word ptr [r11]			;Move the address into a 16 bit register
					 xchg		ch, cl 
					 add		r11, ADDRESSIncrement
					 cmp		bl, ZERO					;Compares the value of register 1 with zero
					 je		    Comparison					;If the comparison is equal to zero jump to the comparison lable 
					 mov		r11, r12					;If not equal to zero the next instruction is att programBuffer+address
					 add		r11, rcx 
					 jmp		Comparison

;***************************************************************************************
;Mnemonic	OpCode(Hex)	Operand 1	Operand 2
;HALT 			FFh								CPU Halts â€“ Terminate program
;***************************************************************************************

Finish:
                    ;Terminate Program
                    mov     ecx,returnCode                  ;parameter 1 contains the return code
                    call    ExitProcess                     ;Windows API - terminates the program

Main                endp

                    END

